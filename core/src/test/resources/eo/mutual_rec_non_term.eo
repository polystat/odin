+package sandbox
+alias stdout org.eolang.io.stdout
+alias sprintf org.eolang.txt.sprintf

[] > base
  memory > x
  [v] > n
    seq > @
      stdout
        sprintf "Calling base.n with v = %d\n" v
      x.write v
  [v] > m
    seq > @
      stdout
        sprintf "Calling base.m with v = %d\n" v
      n v

[] > derived
  base > @
  [v] > n
    seq > @
      stdout (sprintf "Calling derived.n with v = %d\n" v)
      ^.@.m v

[args...] > app
  base > b
  derived > d
  seq > @
    b.n 10
    stdout
      sprintf
        "base:\n\tx after n = %d\n"
        b.x
    b.m 12
    stdout
      sprintf
        "\tx after m = %d\n"
        b.x
    d.n 5
    stdout
      sprintf
        "\nderived:\n\tx after n = %d\n"
        d.x
